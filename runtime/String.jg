# Copyright (c) 2010 Matt Fichman
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, APEXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

String < Object {
    # Represents an immutable character string.  New strings can be created
    # via a substring or concatenation operation on an existing string.

    @init() native {
        # Creates a new empty string
    }

    @index(index Int) native Byte {
        # Returns the character at index index.  If the index refers to a
        # location beyond the length of the string, then
    }

    @add(string String) native String {
        # Concatenates this string with 'string'.
    }

    @equal(string String) native Bool {
        # Returns true if this string is equal to 'string'.
    }

    @less(string String) Bool {
        # Returns true if this string is lexically less than 'string'.
        i mutable = 0
        while i < self.len or i < string.len {
            if self[i] < string[i] {
                ++i
            } else {
                ret false
            }
        }
        ret true
    }

    len?() native Int {
        # Returns the length of the string in charactersk.
    }
    
    slice(begin Int, end Int) native String {
        # Returns a new string containing the elements starting at 'begin' and 
        # ending at the character before 'end'.
    }

    prefix(end Int) String {
        # Returns a new string containing elements starting at the beginning of
        # the string and ending at the character before 'end'.
        ret self.slice(0, end)
    }

    suffix(begin Int) String {
        # Return a new string containing elements starting at 'begin' and 
        # continuing to the end of this string.
        ret self.slice(begin, self.len)
    }

    starts_with(string String) Bool {
        # Returns true if this string starts with the exact string specified
        # by 'string'
        if string.len > self.len {
            ret false
        }
        i mutable = 0
        while i < string.len {
            if string[i] != self[i] {
                ret false
            }    
            ++i
        }
        ret true
    }

    ends_with(string String) Bool {
        # Returns true if this string ends with the exact string specified by
        # 'string'
        if string.len > self.len {
            ret false
        }
        i mutable = 1
        while i <= string.len {
            if string[-i] != self[-i] {
                ret false
            } 
            ++i
        }
        ret true
    }
    
    uppercase?() native String {
        # Returns an uppercase version of this string.
    }

    lowercase?() native String {
        # Returns a lowercase version of this string.
    }

    str?() String {
        ret self
    }

    int?() native Int {
        # Converts the string into an integer.  If the string is not a valid 
        # number, then zero is returned.
    }

    float?() native Float {    
        # Converts the float into an integer.  If the float is not a valid
        # number, then zero is returned.
    }

    byte?() Byte {
        # Converts the string to a character by returning the first character
        # in the string.
        ret self[0]
    }
    
    hash?() native Int {
        # Returns the hash code for this string.
    }

    iter() StringIter {
        # Returns an iterator over the characters in this string
        ret StringIter(self)
    }

    empty?() Bool {
        ret self.len == 0
    }

    findb(start Int, byte Byte) Int {
        # Finds the first occurence of 'byte' starting at position 'start' in
        # the string.
        i mutable = start
        while i < self.len {
            if byte == self[i] {
                ret i 
            }
            ++i
        }
        ret -1
    }

    contains(byte Byte) Bool {
        # Returns true if the string contains the given byte
        ret self.findb(0, byte) >= 0
    }

    split(byte Byte) StringSplitIter {
        # Returns an iterator over substrings formed by splitting the string
        # using the given byte.
        ret StringSplitIter(self, byte)
    }
    
}

StringIter < Object {
    # This iterator returns each of the characters in the string it points to
    # in sequence.

    string private String
    index private = 0

    @init(str String) {
        # Initializes the iterator; the iterator will iterate over the
        # characters in 'str'.
        string = str
    }
    
    more?() Bool {
        # Returns true if there are more elements in the string.
        ret index < string.len
    }

    next() Byte {
        # Returns the next character in the string.
        val = string[index]
        ++index
        ret val
    }

    iter() StringIter {
        ret self
    }
}

StringSplitIter < Object {
    # Returns a series of tokens as split by the given character

    string private String
    byte private Byte
    index private = 0
    last private = -1

    @init(str String, pbyte Byte) {
        string = str
        byte = pbyte
        index = string.findb(index, byte)
    }

    more?() Bool {
        # Returns true if there are more tokens in the string
        ret not not string
    }

    next() String {
        # Returns the next token in the string
        end = if index == -1 { string.len } else { index }
        out = string.slice(last+1, end)
        if index < 0 {
            string = nil
        } else {
            last = index
            index = string.findb(index+1, byte)
        }
        ret out
    }

    iter() StringSplitIter {
        ret self
    }
}
