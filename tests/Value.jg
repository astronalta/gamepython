# jgtest exec on

Printer < Object {
    @destroy() {
        Io::println("destroy obj")
    }
}

Vector < Value {
    @init(x Int, y Int, z Int) {
        # Value type constructors take an extra 'self' pointer to the value
        # type on the stack/embedded in another type.  Likewise, the destructor
        # does not call free().
        #
        # Value types also have an additional hidden copy constructor that
        # increments refcounts for non-weak reference attributes.
        #
        # void Vector__init(Vector self); 
        # void Vector__copy(Vector self, Vector other);
        self.x = x
        self.y = y
        self.z = z
        self.print = Printer()
    }

    @destroy() {
        # Destructors for values types do not call free(); otherwise, they are
        # identical to reference type destructors.
        Io::println("destroy #{x} #{y} #{z}")
    }

    x mutable Int
    y mutable Int
    z mutable Int
    print mutable Printer

}

Complex < Value {
    real mutable Int
    imag mutable Int

    @init(real Int, imag Int) {
        self.real = real
        self.imag = imag
    }
}

Ref < Object {
    complex mutable Vector = Vector(7, 7, 7) 
    bob String 
}

returned() Vector {
    ret Vector(5, 5, 5) # Move semantics?
}

parameter(param Vector) {
    vec = param
    vec.y = 6
    vec.z = 6
}

main() Int {
    # Check some simple stack allocations
    vec1 = Vector(1, 1, 1)
    Io::println("#{vec1.x} #{vec1.y} #{vec1.z}")
    vec2 = Vector(2, 2, 2)
    Io::println("#{vec2.x} #{vec2.y} #{vec2.z}") 

    # Now check allocating in a loop, to be sure we don't run out of mem
    i mutable = 0 
    while i < 1000000 {
        compl = Complex(i, i) 
        ++i
    }
    compl = Complex(42, 42)

    # Anonymous stack alloc
    Vector(3, 3, 3).z = 30 # destroy 3 3 30 + obj

    # Assignment (copy)
    vec3 mutable = vec1
    vec3.z = 10 

    # Assignment (copy, with destructor for prev value)
    vec3 = vec2 # destroy 1 1 10
    vec3.x = 4
    vec3.y = 4
    vec3.z = 4

    val = returned()
    returned() # destroy 5 5 5 + obj
    
    # Pass vector as a parameter
    parameter(vec1) # destroy 1 6 6 

    # Test value type attributes
    ref = Ref()
    ref.complex = Vector(8, 8, 8) 
    # destroy 7 7 7 + obj
    # destroy 8 8 8
    
    x = ref.complex
    x.x = 9
    x.y = 9
    x.z = 9
    
    z mutable = x
    z.x = 10
    z.y = 10
    z.z = 10
    z = ref.complex # destroy 10 10 10
    z.x = 11
    z.y = 11
    z.z = 11
    
    # destroy 8 8 8 (z)
    # destroy 9 9 9 (x) 
    # destroy 8 8 8 (ref) + obj
    # destroy 5 5 5 (val) + obj
    # destroy 4 4 4 (vec3)
    # destroy 2 2 2 (vec2) + obj
    # destroy 1 1 1 (vec1) + obj

    ret 0
}


